apply plugin: "java"

group = "fr.xpdustry"
sourceSets.main.java.srcDirs = ["src/main/java"]


ext{
    // The build number that this plugin is made for
    mindustryVersion = "v126.2"

    // A string that points directly to your server.jar directory
    // the below line is just a way to remind to avoid executing random stuff on your computer
    // the file:// at the beginning is required if you don't want github actions to scream at you
    if(System.getProperty("user.name") == "Finley"){
        serverDirectoryPath = "file://C:/Users/Finley/Documents/My Games/Mindustry/Server/mindustry-server v126.2"
    }else{
        serverDirectoryPath = ""
    }
}

repositories{
    mavenCentral()
    maven{ url "https://www.jitpack.io" }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
}


compileJava{
    sourceCompatibility = 8
    targetCompatibility = 8
    options.encoding = "UTF-8"
}

compileTestJava{
    options.encoding = "UTF-8"
}

jar{
    // The following line is required
    from configurations.runtimeClasspath.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    from "plugin.json"
}


task moveJar{
    dependsOn jar

    doLast{
        if(serverDirectoryPath == null || serverDirectoryPath.isEmpty()){
            throw new GradleException("serverDirectoryPath is unset")
        }

        copy{
            from jar.archiveFile.get()
            into file("$serverDirectoryPath/config/mods")
        }
    }
}

task runServer{
    doLast{
        if(serverDirectoryPath == null || serverDirectoryPath.isEmpty()){
            throw new GradleException("serverDirectoryPath is unset")
        }

        exec{
            workingDir = file(serverDirectoryPath)
            commandLine = ["cmd", "/C", "start", "run_server.bat"]
        }
    }
}

task deploy{
    dependsOn moveJar
    dependsOn runServer
}

// Required if you want to use the Release GitHub action,
task getArtifactPath{
    doLast{
        println jar.archiveFile.get().toString()
    }
}
